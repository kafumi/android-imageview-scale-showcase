apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.facebook.testing.screenshot'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.github.kafumi.imageviewscaleshowcase"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "io.github.kafumi.imageviewscaleshowcase.ScreenshotTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        animationsDisabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.0.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}

task cleanScreenshots(type: Delete) {
    delete 'screenshots'
}
task moveScreenshots(type: Copy, dependsOn: cleanScreenshots) {
    from('build/screenshotsDebugAndroidTest') {
        include 'screenshot*.png'
    }
    into 'screenshots'
}
afterEvaluate {
    pullDebugAndroidTestScreenshots.finalizedBy moveScreenshots
    runDebugAndroidTestScreenshotTest.finalizedBy moveScreenshots
    verifyDebugAndroidTestScreenshotTest.finalizedBy moveScreenshots
}
